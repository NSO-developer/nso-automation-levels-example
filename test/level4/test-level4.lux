[doc Test service (level 3)]

[config require=MAKE]

[global fail_pattern=[Ee][Rr][Rr][Oo][Rr]]

[shell ncs]
    [timeout 30]
    !${MAKE} start
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT

[shell ncs-cli]
    -$fail_pattern
    !${MAKE} cli
    ?admin connected
    ?admin@.*>

    !set paginate false
    ??[ok]
    ?admin@.*>

    !configure
    ?admin@.*%

    !request devices sync-from
    ??[ok]
    ?admin@.*%

    !set dc dc0 fw fw0 media-origin mediaorigin0 skylight skylight0
    ??[ok]
    !set dc dc1 fw fw1 media-origin mediaorigin1 skylight skylight1
    ??[ok]
    !commit
    ??Commit complete

[shell ncs_cmd]
    !ncs_cmd -c "dbset operational; mtrans; mset /streaming:dc{dc0}/oper-status/jitter 30;"
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT
    !ncs_cmd -c "dbset operational; mtrans; mset /streaming:dc{dc1}/oper-status/jitter 10;"
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT

[shell ncs-cli]
    !set edge edge0
    ??[ok]
    !commit
    ??Commit complete

    !run show edge plan | notab
"""?
edge edge0
 plan component self self
  state init
   status reached
   when   .*T.*:.*:[0-9]*
  state ready
   status reached
   when   .*T.*:.*:[0-9]*
  back-track false
 plan component dc dc1
  state init
   status reached
   when   .*T.*:.*:[0-9]*
  state skylight-configured
   status reached
   when   .*T.*:.*:[0-9]*
  state media-origin-monitored
   status reached
   when   .*T.*:.*:[0-9]*
  state fw-configured
   status reached
   when   .*T.*:.*:[0-9]*
  state ready
   status reached
   when   .*T.*:.*:[0-9]*
  back-track false
 plan component edge edge
  state init
   status reached
   when   .*T.*:.*:[0-9]*
  state connected-to-dc
   status reached
   when   .*T.*:.*:[0-9]*
  state connected-to-skylight
   status reached
   when   .*T.*:.*:[0-9]*
  state ready
   status reached
   when   .*T.*:.*:[0-9]*
  back-track false
"""

[shell ncs_cmd]
    !ncs_cmd -c "dbset operational; mtrans; mset /streaming:dc{dc1}/oper-status/jitter 150;"
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT

[shell ncs-cli]
    ?Commit performed by admin
    !run show edge plan | notab
"""?
edge edge0
 plan component self self
  state init
   status reached
   when   .*T.*:.*:[0-9]*
  state ready
   status reached
   when   .*T.*:.*:[0-9]*
  back-track false
 plan component dc dc0
  state init
   status reached
   when   .*T.*:.*:[0-9]*
  state skylight-configured
   status reached
   when   .*T.*:.*:[0-9]*
  state media-origin-monitored
   status reached
   when   .*T.*:.*:[0-9]*
  state fw-configured
   status reached
   when   .*T.*:.*:[0-9]*
  state ready
   status reached
   when   .*T.*:.*:[0-9]*
  back-track false
 plan component edge edge
  state init
   status reached
   when   .*T.*:.*:[0-9]*
  state connected-to-dc
   status reached
   when   .*T.*:.*:[0-9]*
  state connected-to-skylight
   status reached
   when   .*T.*:.*:[0-9]*
  state ready
   status reached
   when   .*T.*:.*:[0-9]*
  back-track false
"""

[cleanup]
    !${MAKE} stop
    !echo ==$$?==
    ?==[0-9]+==
